############################################################
# Description: Kills the player if their health is too low
# Creator: CreeperMagnet_
############################################################

execute store result score @s nex.data run gamerule showDeathMessages
execute if entity @s[scores={nex.data=1}] run gamerule showDeathMessages false
kill @s[tag=!global.ignore.kill]
execute if entity @s[scores={nex.data=1},tag=nex.damage.boomerang] run tellraw @a {"translate":"death.tcc.boomerang","with":[{"selector":"@s"}]}
execute if entity @s[scores={nex.data=1},tag=nex.damage.iceologer_ice] run tellraw @a {"translate":"death.tcc.iceologer_ice","with":[{"selector":"@s"}]}
execute if entity @s[scores={nex.data=1},tag=nex.damage.iceologer_freeze] run tellraw @a {"translate":"death.tcc.iceologer_freeze","with":[{"selector":"@s"}]}
execute if entity @s[scores={nex.data=1},tag=nex.damage.swordfish] run tellraw @a {"translate":"death.tcc.swordfish","with":[{"selector":"@s"}]}
execute if entity @s[scores={nex.data=1},tag=nex.damage.geomancer_explosive] run tellraw @a {"translate":"death.tcc.geomancer_explosive","with":[{"selector":"@s"}]}
execute if entity @s[scores={nex.data=1},tag=nex.damage.channeling_lightning] run tellraw @a {"translate":"death.tcc.channeling_lightning","with":[{"selector":"@s"}]}
execute if entity @s[scores={nex.data=1}] run gamerule showDeathMessages true
