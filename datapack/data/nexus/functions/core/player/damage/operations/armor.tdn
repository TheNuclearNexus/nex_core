############################################################
# Description: Calculates armor into the damage formula
# Creator: ICY / CreeperMagnet_
############################################################

# nex.damage nex.data: damage to add to entity, to 1 decimal place (x10)


  ## https://static.wikia.nocookie.net/minecraft_gamepedia/images/0/0e/ArmorDamageFormula.svg/revision/latest/scale-to-width-down/668?cb=20190728042948
  ## FORMULA:
  ## damage = damage * (1 - (min(20,max(armor ÷ 5 , ATF))) ÷ 25)


 ## Insert Armor Toughness
  execute store result score temp1 nex.data run attribute @s minecraft:generic.armor_toughness get 10

  # Create value for first portion of max(armor ÷ 5, ATF)
  # This leads to armor ÷ 5 being stored in temp2
scoreboard players operation temp2 nex.data = temp0 nex.data
set temp2->nex.data /= 5

 ##### SEPARATOR SO YOUR BRAIN DOESN'T MELT

  # Create values for second portion of max(armor ÷ 5, ATF) (ATF)
  # This is the armor toughness formula calculation. It is nasty.
  # It is equal to: (armor - damage ÷ (2 + (toughness ÷ 4)))



  # Assign Temporary Values from the semi-permanent values.

  # Armor
scoreboard players operation temp3 nex.data = temp0 nex.data
  # Damage
scoreboard players operation temp4 nex.data = nex.damage nex.data
  # Armor Toughness
scoreboard players operation temp5 nex.data = temp1 nex.data


  # Divide by 4 (working inside -> out), so (toughness ÷ 4) first.
  # tcc.const.4 always has the value of 4.

set temp5->nex.data /= 4 


  # Then we add 2 to that, again working inside -> out. (2 + (toughness ÷ 4))

scoreboard players add temp5 nex.data 20


  # Then, divide the damage by that value. (damage ÷ (2 + (toughness ÷ 4)))

scoreboard players operation temp4 nex.data /= temp5 nex.data
set temp4->nex.data *= 10

  # Then subtract that value from armor, and you have the final result of the armor toughness formula.

scoreboard players operation temp3 nex.data -= temp4 nex.data

 ##### SEPARATOR SO YOUR BRAIN DOESN'T MELT



 # The next portion of this massive garbage heap of a formula. The max(armor ÷ 5, ATF). We've already calculated both, so we can just put them in here, and take the greatest one.
 # temp2 = armor ÷ 5
 # temp3 = Armor Toughness Formula results

execute if score temp3 nex.data > temp2 nex.data run scoreboard players operation temp2 nex.data = temp3 nex.data


 # Then the next part, the min(20, max(whatever was in last step)). If >20, it equals 20.
 # temp2 = The results from last step, carried over.

execute if score temp2 nex.data matches 200.. run scoreboard players set temp2 nex.data 200



 # The REALLY MESSY MATH

  # Multiply by 10 to maintain precision
set temp2->nex.data *= 10 
  # Divide by 25 per the formula
set temp2->nex.data /= 25 

  # Then subtract it all from 1 (100)

scoreboard players set temp0 nex.data 100
scoreboard players operation temp0 nex.data -= temp2 nex.data

  # Multiply the damage by the end result, then divide by 100 to get proper precision back.
scoreboard players operation nex.damage nex.data *= temp0 nex.data
set nex.damage->nex.data /= 100
